plugins {
	id 'org.springframework.boot' version '2.2.2.RELEASE'
	id 'io.spring.dependency-management' version '1.0.8.RELEASE'
	id 'java'
	id 'jacoco'
}

group = 'com.ermakov.nikita'
version = (project.hasProperty('releaseTag') && project['releaseTag'] != '') ? project['releaseTag'] : 'SNAPSHOT'
sourceCompatibility = '1.8'

springBoot {
	mainClassName = 'com.ermakov.nikita.smokecloud.SmokeCloudApplication'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'com.github.ulisesbocchio:jasypt-spring-boot-starter:2.1.2'
	implementation 'org.liquibase:liquibase-core:3.8.6'

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.springframework.security:spring-security-test'

	if (project.hasProperty('profile') && project['profile'] != '') {
		logger.info('Using postgre database')
		runtimeOnly 'org.postgresql:postgresql'
	} else {
		logger.info('Spring profile not specified. Using embedded h2 database')
		runtimeOnly 'com.h2database:h2'
	}
}

test {
	useJUnitPlatform()
}

jacocoTestReport {
	reports {
		xml.enabled true
	}
}

check {
	dependsOn jacocoTestReport
}