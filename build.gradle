plugins {
    id 'org.springframework.boot' version '2.2.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
    id 'jacoco'
}

springBoot {
    mainClassName = 'com.ermakov.nikita.smokecloud.SmokeCloudApplication'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'com.github.ulisesbocchio:jasypt-spring-boot-starter:2.1.2'
    //    implementation 'org.liquibase:liquibase-core:3.8.6'
//	implementation 'org.yaml:snakeyaml:1.25'

    implementation project(':security')
    implementation project(':controller')

    runtimeOnly 'org.postgresql:postgresql'
    if (!project.hasProperty('profile') || !project['profile'].equals('prod')) {
        runtimeOnly 'com.h2database:h2'
    }
}

allprojects {
    group = 'com.ermakov.nikita'
    version = (project.hasProperty('releaseTag') && project['releaseTag'] != '') ? project['releaseTag'] : 'SNAPSHOT'
    sourceCompatibility = '1.8'

    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'
    apply plugin: 'java'

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.12'

        testImplementation 'org.mockito:mockito-core:2.10.0'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
        testImplementation 'org.springframework.security:spring-security-test'
    }

    repositories {
        mavenCentral()
    }

    test {
        useJUnitPlatform()
    }

    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }

    check {
        dependsOn jacocoTestReport
    }
}

subprojects {
    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }
}