plugins {
    id 'org.springframework.boot' version '2.2.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id 'jacoco'
}

group = 'com.ermakov.nikita'
version = (project.hasProperty('releaseTag') && project['releaseTag'] != '') ? project['releaseTag'] : 'SNAPSHOT'
sourceCompatibility = '1.8'

springBoot {
    mainClassName = 'com.ermakov.nikita.smokecloud.SmokeCloudApplication'
}

dependencies {
    implementation project(':application')
}

allprojects {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'
    apply plugin: 'java'

    dependencyManagement {
        dependencies {
            dependency 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5:3.0.4.RELEASE'
            dependency 'com.github.ulisesbocchio:jasypt-spring-boot-starter:2.1.2'
            dependency 'org.liquibase:liquibase-core:3.8.6'
            dependency 'org.yaml:snakeyaml:1.25'
            dependency 'org.projectlombok:lombok:1.18.12'
            dependency 'javax.validation:validation-api:2.0.1.Final'
            dependency 'org.apache.commons:commons-lang3:3.9'
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
        testImplementation 'org.springframework.security:spring-security-test'

        annotationProcessor 'org.projectlombok:lombok'
    }

    repositories {
        mavenCentral()
    }

    test {
        useJUnitPlatform()
    }

    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }

    check {
        dependsOn jacocoTestReport
    }
}

subprojects {
    group = 'com.ermakov.nikita'
    sourceCompatibility = '1.8'

    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }
}