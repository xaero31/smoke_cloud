plugins {
    id 'org.springframework.boot' version '2.2.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
    id 'jacoco'
}

group = 'com.ermakov.nikita'
version = (project.hasProperty('releaseTag') && project['releaseTag'] != '') ? project['releaseTag'] : 'SNAPSHOT'
sourceCompatibility = '1.8'

springBoot {
    mainClassName = 'com.ermakov.nikita.smokecloud.SmokeCloudApplication'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5:3.0.4.RELEASE'
    implementation 'com.github.ulisesbocchio:jasypt-spring-boot-starter:2.1.2'
    implementation 'org.liquibase:liquibase-core:3.8.6'
	implementation 'org.yaml:snakeyaml:1.25'

    implementation project(':model')
    implementation project(':security')
    implementation project(':controller')
    implementation project(':database')

    runtimeOnly 'org.postgresql:postgresql'
    if (!project.hasProperty('profile') || !project['profile'].equals('prod')) {
        runtimeOnly 'com.h2database:h2'
    }
}

allprojects {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'
    apply plugin: 'java'

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.12'

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
        testImplementation 'org.springframework.security:spring-security-test'

        annotationProcessor 'org.projectlombok:lombok'
    }

    repositories {
        mavenCentral()
    }

    test {
        useJUnitPlatform()
    }

    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }

    check {
        dependsOn jacocoTestReport
    }
}

subprojects {
    group = 'com.ermakov.nikita'
    version = (project.hasProperty('releaseTag') && project['releaseTag'] != '') ? project['releaseTag'] : 'SNAPSHOT'
    sourceCompatibility = '1.8'

    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }
}